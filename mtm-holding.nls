holdings-own [
  the-land                 ;; patch-set of patches of this farmer, this farm, or this holding
  farm-type                ;; farm type of farm - one of farm-types global

  my-farmer                ;; farmer who owns/runs farm
  my-farm
  
  landuse-luc-profile      ;; matrix of counts of LUC - farm-type (i.e. landuse) combinations of the-land

  current-profit           ;; most recent profit of farm summed across patches
  current-income           ;; most recent income of farm summed across patches
  current-costs            ;; most recent costs of farm summed across patches
  
  my-interventions         ;; row matrix of 0/1s indicating adoption of intervention-types
  avail-interventions      ;; row matrix of 0/1s indicating interventions still open for this farm-type
  metrics-details
  my-metrics

  farm-type-0              ;; variables at model initialisation for quick reset
  landuse-luc-profile-0
  current-profit-0
  current-income-0
  current-costs-0
  my-interventions-0
  avail-interventions-0
  my-metrics-0
]


;; farm 'constructor'
to initialise-holding
  ;; will be called by the farmer which is 'myself' in this context
  set my-farmer myself
  set my-farm [my-farm] of my-farmer
  move-to approximate-centroid the-land
  set shape "square 3"
  set hidden? false
  set label farm-type
  set label-color ifelse-value show-labels? [table:get colour-key "label"] [[0 0 0 0]]
  set metrics-details table:make
  set my-metrics table:make
end

to initialise-holding-interventions
  ;; interventions are a 1 row matrix of 0s to start
  set my-interventions matrix:make-constant 1 (length interventions) 0
  set avail-interventions get-possible-interventions
  let starter-props map [intervention -> 
    table:get (
      table:get (
        table:get intervention-properties "init_proportion"
       ) farm-type
     ) intervention
  ] interventions
  (foreach interventions starter-props [ [i p] ->
    if random-float 1 < p [ do-intervention i ]
  ]) 
end

;; does the book keeping if the farm-type is changed
to change-holding-farm-type [ft]
  set farm-type ft
  ask the-land [set landuse ft]
  set landuse-luc-profile get-landuse-luc-profile the-land
  set my-interventions (matrix:map * get-possible-interventions my-interventions) 
  set avail-interventions matrix:map [x -> ifelse-value x < 1 [0] [1]] 
                          (get-possible-interventions matrix:- my-interventions) 
end

;; reports a column matrix from the column sums of current landuse-luc-profile matrix
;; holding reporter
to-report get-landuse-profile
  report matrix:from-column-list (list matrix-column-sums landuse-luc-profile)
end

;; reports a column matrix from the row sums of current landuse-luc-profile matrix
;; holding reporter
to-report get-luc-profile
  report matrix:from-column-list (list matrix-row-sums landuse-luc-profile)
end

;; reports a 8 row 4 col matrix of the count of patches with each combination of
;; LUC code and landuse/farm-type in the patch-set poly. Usually this is called 
;; by a holding, when poly will be 'the-land'. Initially all patches will be the 
;; same landuse matching the farm-type, but some farmer actions may lead to this 
;; changing. Run during initialisation and only altered if such changes in landuse 
;; are undertaken.
;; not a holding reporter but usually run on the-land of holding turtles
to-report get-landuse-luc-profile [poly]
  let luc-i [position luc-code range-from-to 1 9] of poly
  let lu-j  [position landuse farm-types] of poly
  let nrow 8
  let ncol length farm-types
  let farm-profile matrix:make-constant nrow ncol 0
  (foreach luc-i lu-j [ [i j] ->
    matrix:set farm-profile i j (matrix:get farm-profile i j) + hectares-per-patch
  ])
  report farm-profile
end

;; for this farm's type returns 0/1 row matrix indicating which are 
;; possible on this type of farm
to-report get-possible-interventions
  report row-matrix map [
    i -> ifelse-value (is-applicable-to-landuse? i and
                       is-possible-on-this-holding? i) [1] [0]] interventions
end

;; update my-interventions and available-interventions matrices
;; to reflect implementation of intervention i-type
to do-intervention [i-type]
  let col position i-type interventions
  matrix:set my-interventions 0 col 1
  matrix:set avail-interventions 0 col 0
  ;; also update the interventions of the parent farmer
  let row position farm-type farm-types
  ask my-farmer [ matrix:set my-interventions row col matrix:get my-interventions row col + 1 ]
  ;; and add to debt of farm
  let cost hectares-per-patch * 
           count the-land * 
           table:get (
             table:get (
               table:get intervention-properties "capital_costs"
             ) farm-type 
           ) i-type
  ask my-farmer [ add-to-debt cost]
end

;; returns true if there are any remaining available interventions
;;
;; TODO: check if this is still correct given max_proportion input to model
;;
to-report any-interventions-left-to-do?
  report matrix-sum-elements avail-interventions > 0
end

;; tests if intervention is applicable by checking if the result is in
;; interventions table not a holding reporter but most often run by a holding turtle
to-report is-applicable-to-landuse? [i-type]
  report get-impact farm-type "costs" i-type != na-value
end

to-report is-possible-on-this-holding? [i-type]
  report random-float 1 < 
    table:get (
      table:get (
        table:get intervention-properties "max_proportion"
      ) farm-type
    ) i-type
end

;; returns impact by farm-type, effect-type, and intervention-type
;; not a holding reporter but most often run by a holding turtle
to-report get-impact [fm-type effect-type intervention-type]
  report table:get (
           table:get (
             table:get intervention-properties effect-type
           ) fm-type
         ) intervention-type
end

;; reports my-interventions as a list of intervention-type names for convenience
;; holding reporter
to-report get-my-interventions-list
  ;; slice by ones returns items in the first list that match 1s in the second
  ;; so e.g. ["A" "B" "C" "D"] [0 1 1 0] -> ["B" "C"]
  report slice-by-ones interventions (matrix:get-row my-interventions 0)
end

;; reports available-interventions as a list of intervention-type names for 
;; convenience -- a holding reporter
to-report get-available-interventions-list
  report slice-by-ones interventions (matrix:get-row avail-interventions 0)
end

;; scores a potential intervention by comparing expected income and costs
;; were it to be implemented relative to current income and costs
;; TODO: consider if this actually makes sense... in a good year when current
;; income is high and current costs low the score might be lower which is
;; slightly counter-intuitive.
;; a holding reporter because cost and income reporters invoked are holding reporters
to-report get-intervention-score [i-type]
  let col (position i-type interventions)
  matrix:set my-interventions 0 col 1
  ;; get long-term expect costs and incomes, i.e. without variance
  let new-costs    get-holding-costs false
  let new-income   get-holding-income false
  let delta-costs  relative-change current-costs new-costs
  let delta-income relative-change current-income new-income
  if show-events? [
    show i-type
    show (word "Current costs: " current-costs " Current income: " current-income)
    show (word "New costs    : " new-costs     " New income    : " new-income)
    show (word "Delta costs  : " delta-costs   " Delta income  : " delta-income)
  ]
  ;; undo the intervention... we are only trying them on for size here!
  matrix:set my-interventions 0 col 0
  report delta-income - delta-costs
end

;; wrapper to invoke the various profit and loss reporters
to update-holding-income-costs-profit
  set current-income get-holding-income true
  set current-costs  get-holding-costs true
  set current-profit current-income - current-costs
end

to update-holding-environmental-metrics
  foreach sort table:keys env-metrics [ metric ->
    table:put metrics-details metric matrix:map [x -> precision x 3]
    get-holding-environmental-metric-incl-variance metric
    set-metric metric
  ]
end

to set-metric [metric]
  let value matrix-sum-elements (
    matrix:map * landuse-luc-profile table:get metrics-details metric
  )
  table:put my-metrics metric precision value 3
end

;; ----------------------------------------------------------------------------
;; matrix calculations of holding income etc.
;; ----------------------------------------------------------------------------
;; these are holding reporters because they rely on the luc-landuse-profile matrix
;; which it would be inefficient to reassemble on the fly and is updated rarely

;; holding reporter due to landuse-luc-profile
to-report get-holding-income [include-variance?]
  ;; calculation here is SUM [ LUC/LU * Yield * Impact of interventions * Prices ]
  ;; Yield component optionally includes variance
  ;; Multiplications are all element-wise except for last step * Prices
  ifelse include-variance? [
    report round matrix-sum-elements (
      ( matrix:map [[a b c] -> a * b * c] 
          landuse-luc-profile
          get-holding-yield-incl-variance
          get-yield-after-interventions 
      ) matrix:* prices
    )
  ]
  [ 
    report round matrix-sum-elements (
      ( matrix:map [[a b c] -> a * b * c]
          landuse-luc-profile
          yield-means  ;; no variance
          get-yield-after-interventions
      ) matrix:* prices
    )
  ]
end

;; holding reporter due to landuse-luc-profile
to-report get-holding-costs [include-variance?]
  ;; calculation here is
  ;; Cost of interventions + SUM [ LUC/LU * Costs + Taxes ]
  ;; Costs optionally include variance
  ifelse include-variance? [
    report round ( 
      get-cost-after-interventions + 
      matrix-sum-elements (
        ( matrix:map * landuse-luc-profile get-holding-cost-incl-variance )
        matrix:+
        get-net-environmental-taxes
      )
    )
  ]
  [ 
    report round ( 
      get-cost-after-interventions + 
      matrix-sum-elements (
        ( matrix:map * landuse-luc-profile cost-means )
        matrix:+ 
        get-net-environmental-taxes
      ) 
    )
  ]
end

to-report get-net-environmental-taxes
  let taxed-metrics filter [
    k -> table:get table:get env-metrics k "price" != na-value
  ] table:keys env-metrics
  let taxes map [m -> table:get (table:get env-metrics m) "price"] taxed-metrics
  report matrix:map [m -> round m] 
           reduce [[a b] -> a matrix:+ b] 
             (map [[m t] -> ( matrix:map * 
                              landuse-luc-profile
                              table:get metrics-details m ) matrix:* t] 
              taxed-metrics taxes)
end

;; Reduced variance normal deviates for sample size n
;; This simulates the central tendency effect of deviations from mean
;; across farms of varying size. So a small farm with only a single patch
;; of given LU/LUC might see a very large negative or positive deviation
;; from mean outcomes. But a large farm with n patches of LU/LUC has the
;; deviation downscaled by âˆšn -- this is of course summed across n patches
;; so the overall deviation will be larger, but appropriately 'suppressed'
;; for the larger holding.
to-report reduced-normal-deviate [mn sd n]
  ( ifelse
    n = 0 [ report 0 ] ;; n = 0 is meaningless
    n = 1 [ report random-normal mn sd ]
    [ report random-normal mn (sd / sqrt(n)) ]
  )
end

to-report get-holding-yield-incl-variance
  report (
    matrix:map [[m s n] -> reduced-normal-deviate m s n] 
                  yield-means 
                  yield-sds 
                  landuse-luc-profile
  )
end

to-report get-holding-cost-incl-variance
  report (
    matrix:map [[m s n] -> reduced-normal-deviate m s n]
                  cost-means
                  cost-sds
                  landuse-luc-profile
  )
end

;; reports a matrix of emissions deviates suppressed by the number of patches
;; in accordance with the central limit theorem...
;; holding reporter due to landuse-luc-profile
to-report get-holding-environmental-metric-incl-variance [metric]
  report (
    matrix:map [[m s n] -> reduced-normal-deviate m s n] 
                  table:get env-metric-means metric 
                  table:get env-metric-sds metric 
                  landuse-luc-profile
  )
end

;; holding reporter due to get-farm-landuse-profile
to-report get-cost-after-interventions
  ;; the calculation here is
  ;;                       [       costs        ]   [   col-matrix    ]
  ;; [interventions 0/1] * [ rows: intervention ] * [                 ]
  ;;                       [ cols: farm-type    ]   [ rows: farm-type ]
  ;;
  report round matrix-sum-elements
         ( my-interventions matrix:*
           intervention-cost-impacts matrix:*
           get-landuse-profile )
end

;; reports a matrix for use in multiplication of yields dependent on on-farm 
;; interventions. This is a non-standard matrix operation where desired result 
;; is the cumulative product of each column in the matrix but not all row 
;; entries in the matrix are applicable (because not a all interventions will 
;; have been adopted).
;;
;; holding reporter due to my-interventions
to-report get-yield-after-interventions
  report matrix:transpose
    matrix-duplicate-cols (
      matrix-column-products ( ;; cumulative product of adopted impacts
      ( matrix:map [[a b] -> ifelse-value a = 0 [1] [b]] ;; map non-adopted interventions to 1
        matrix-duplicate-cols matrix:transpose my-interventions length farm-types ;; dup for each farm-type
        intervention-yield-impacts )
      )
    ) 8 ;; duplicated for each LUC
end

;; as above but for emissions
;;
;; holding reporter due to my-interventions
to-report get-environmental-metric-after-interventions [metric]
  report matrix:transpose
    matrix-duplicate-cols (
      matrix-column-products (
      ( matrix:map [[a b] -> ifelse-value a = 0 [1] [b]]
        matrix-duplicate-cols matrix:transpose my-interventions length farm-types
        table:get intervention-env-impacts metric)
      )
    ) 8
end

to-report get-gross-income
  report round (
    matrix-sum-elements (
      (matrix:map [[a b] -> a * b] landuse-luc-profile yield-means) 
      matrix:* prices)
  )
end

to-report get-gross-costs
  report round (
    matrix-sum-elements (
      matrix:map [[a b] -> a * b] landuse-luc-profile cost-means)
  )
end

to-report get-gross-return
  report round (get-gross-income - get-gross-costs)
end

;;
;; DEBUG FILE OUTPUT
;; Note this preferred to using simpleR extension to avoid dependency on R installation
to output-holdings-report [fname]
  csv:to-file fname fput get-holding-csv-header map [h -> [get-holding-status] of h] sort holdings
end

to-report get-holding-status
  report flatten-list (list 
    who
    [who] of my-farmer
    [who] of my-farm
    farm-type 
    [luc-code] of one-of the-land
    (count the-land * hectares-per-patch)
    (map [x -> ifelse-value x = 1 ["TRUE"] ["FALSE"]] matrix:get-row my-interventions 0)
    current-income 
    current-costs 
    current-profit
    [get-holding-income false] of self
    [get-holding-costs false] of self
    [get-gross-income] of self
    [get-gross-costs] of self
    [get-gross-return] of self
  )
end

to-report get-holding-csv-header
  report flatten-list (list
    "holding_id"
    "farmer_id"
    "farm_id"
    "farm_type"
    "LUC"
    "size_ha"
    interventions
    "current_income"
    "current_costs"
    "current_profit"
    "current_income_no_variance"
    "current_costs_no_variance"
    "gross_income"
    "gross_costs"
    "gross_return"
  )
end


;; The MIT License (MIT)
;;
;; Copyright (c) 2023-25 David O'Sullivan
;;
;; Permission is hereby granted, free of charge, to any person
;; obtaining a copy of this software and associated documentation
;; files (the "Software"), to deal in the Software without restriction,
;; including without limitation the rights to use, copy, modify, merge,
;; publish, distribute, sublicense, and/or sell copies of the Software,
;; and to  permit persons to whom the Software is furnished to do so,
;; subject to the following conditions:
;;
;; The above copyright notice and this permission notice shall be included
;; in all copies or substantial portions of the Software.
;;
;; THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS
;; OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
;; FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL
;; THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
;; LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
;; FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER
;; DEALINGS IN THE SOFTWARE.


