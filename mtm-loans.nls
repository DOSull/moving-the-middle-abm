;; reports annual repayments on a loan of given principal 
;; amount ($), interest rate (%), and term (years)
to-report get-loan-repayments [P i-rate loan-term]
  let r i-rate / 100
  ifelse r = 0 [
    report round (P / loan-term)
  ]
  [
    let x (1 + r) ^ loan-term
    report round (P * r * x / (x - 1))
  ]
end

;; reports princial amount based on the payment ($) of
;; at a given interest rate (%) and term (years)
;;
;; this is required when setting inital indebtedness of
;; farms based on a known debt-service-ratio
to-report get-principal-from-payment [pmt i-rate loan-term]
  let r i-rate / 100
  ifelse r = 0 [
    report round (pmt * loan-term)
  ]
  [
    let x (1 + r) ^ loan-term
    report round (pmt * (1 - (1 / x)) / r)
  ]
end

;; reports principal outstanding on a long og given
;; principal ($), interest rate (%), loan term (years) and
;; maturaity, i.e. years into paying off loan
to-report get-principal-remaining [P i-rate loan-term m]
  if m = nobody [ report 0 ]
  let r i-rate / 100
  ifelse r = 0 [
    report round (P * (loan-term - m) / loan-term)
  ]
  [
    let pmt get-loan-repayments P i-rate loan-term
    let x (1 + r) ^ m
    report round (P * x - pmt * ((x - 1) / r))
  ]
end
