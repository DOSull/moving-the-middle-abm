;; -----------------------------------------
;; farmer (behavioural) parameter setup
;; -----------------------------------------

;; we read the interventions by farm types file twice...
to setup-farmer-parameters
  ;; Assumes a CSV organised with farm types as column headings,
  ;; interventions as row names, i.e, (spaces added for clarity)
  ;;
  ;;                  , SNB, Dairy, Forest, Crop
  ;; Build_Wetland    , 0.7, 0.75,  0.3,    0.5
  ;; Riparian_Planting, 0.7, 0.75,  0.2,    0.4
  ;; Clean_Races      , 0.2, 0.7,   0,      0
  ;; Farm_Plan        , 0.7, 0.85,  0.4,    0.6
  ;; Join_ETS         , 0.2, 0.2,   0.9,    0.4
  ;;
  read-farm-types-from-file word market-data-folder "farmer-threshold-matrix.csv"
  read-interventions-from-file word market-data-folder "farmer-threshold-matrix.csv"
  read-farmer-thresholds-from-file word market-data-folder "farmer-threshold-matrix.csv"
  read-intervention-impacts-from-file word market-data-folder "intervention-impacts.csv"
end

;; setup farm types and interventions
to read-farm-types-from-file [file]
  carefully [
    print word "Reading farm types from " file
    file-open file
    set farm-types sort but-first csv:from-row file-read-line
    file-close
  ]
  [
    print "ERROR: problem reading farm types from thresholds data file"
    file-close
  ]
end

;; setup interventions
to read-interventions-from-file [file]
  let names []
  carefully [
    print word "Reading intervention types from " file
    file-open file
    let skip file-read-line
    while [not file-at-end?] [
      set names lput item 0 csv:from-row file-read-line names
;      create-interventions 1 [
;        initialise-intervention item 0 csv:from-row file-read-line
;      ]
    ]
    file-close
    set intervention-types sort names
    foreach intervention-types [ i -> output-print i ]
  ]
  [
    print "ERROR: problem reading intervention types from thresholds data file"
    file-close
  ]
end

;; setup baseline probability of adoption of interventions by farm type
to read-farmer-thresholds-from-file [file]
  ;; CSV file looks like this (spacing added for clarity)
  ;;
  ;;                   , SNB, Dairy, Forest, Crop
  ;;  Build_Wetland    , 0.7,  0.75,    0.3,  0.5
  ;;  Riparian_Planting, 0.7,  0.75,    0.2,  0.4
  ;;  Clean_Races      , 0.2,  0.7 ,    0  ,  0
  ;;  Farm_Plan        , 0.7,  0.85,    0.4,  0.6
  ;;  Join_ETS         , 0.2,  0.2 ,    0.9,  0.4
  ;;
  ;; resulting table is read by
  ;;   table-get (table:get base-thresholds INTERVENTION-NAME) FARM-TYPE
  set base-thresholds table:make
  carefully [
    print word "Reading farmer adoption thresholds from file " file
    file-open file
    let the-farm-types but-first csv:from-row file-read-line
    let order get-order the-farm-types farm-types
    while [not file-at-end?] [
      let data csv:from-row file-read-line
      let the-intervention item 0 data
      let vals table:from-list reorder zip the-farm-types (but-first data) order
      table:put base-thresholds the-intervention vals
    ]
    file-close
  ]
  [
    print "ERROR: problem reading the farmer thresholds data file"
    file-close
  ]
end

;; initialise the global intervention-effects table
;; which holds production function impacts by intervention type, effect, and farm type
to read-intervention-impacts-from-file [file]
  ;; CSV file format like this - note that each intervention should be
  ;; 3 consecutive lines with its effects (the three impacts can be in 
  ;; any order, but each set should be IN THE SAME ORDER)
  ;; farm types can be in any order. Spaces added for clarity
  ;;
  ;; Intervention     , Impact   ,   SNB, Dairy, Forest,  Crop
  ;; Build_Wetland    , costs    ,    25,    68,     NA,    34
  ;; Build_Wetland    , yields   ,  -0.2, -0.02,     NA,  -0.1
  ;; Build_Wetland    , emissions,  -0.1, -0.05,     NA, -0.05
  ;; Riparian_Planting, costs    ,    26,    71,     NA,    11
  ;; Riparian_Planting, yields   ,  -0.2, -0.02,     NA, -0.01
  ;; Riparian_Planting, emissions,  -0.1, -0.03,     NA, -0.04
  ;;  ... and so on ...
  ;;
  let i-list []   ;; list of tables, one per intervention type
  let i-types []  ;; list of intervention types in the order they appear 
  carefully [
    print word "Reading intervention impacts from " file
    file-open file
    let the-farm-types but-first but-first csv:from-row file-read-line
    let order get-order the-farm-types farm-types
    while [not file-at-end?] [
      let i 0
      let i-type nobody
      let e-types []
      let e-impacts []
      while [i < 3] [ ;; read the table by 3s
        let data csv:from-row file-read-line
        set i-type item 0 data
        if i = 0 [set i-types fput i-type i-types]
        set data but-first data
        let e-type item 0 data
        set e-types lput e-type e-types
        set e-impacts lput table:from-list 
          reorder zip the-farm-types (but-first map [x -> replace-na x] data) order e-impacts
        set i i + 1
      ]
      set i-list fput table:from-list zip e-types e-impacts i-list
    ]
    file-close
    set order get-order i-types intervention-types
    set interventions table:from-list reorder zip i-types i-list order
  ]
  [
    print "ERROR: problem reading the interventions effects file"
    file-close
  ]
end

to-report replace-na [x]
  report ifelse-value x = "NA" [na-value] [x]
end

to-report replace [x y z]
  report ifelse-value x = y [z] [x]
end 


;; -----------------------------------------
;; economic setup procedures
;; -----------------------------------------
to setup-economic-parameters
  with-local-randomness [
    ifelse seed-setup-rng?
    [ random-seed rng-economics ]
    [ random-seed timer ]
    read-production-function-parameters
    ask farms [ set landuse-luc-profile get-farm-landuse-luc-profile the-land ]
    make-matrix-copies-of-data
  ]
end

to read-production-function-parameters
  let yields get-parameter-from-file word market-data-folder "commodity-yields.csv"
  set commodity-yield-means item 0 yields
  set commodity-yield-sds   item 1 yields

  let costs get-parameter-from-file word market-data-folder "input-costs.csv"
  set input-cost-means      item 0 costs
  set input-cost-sds        item 1 costs

  let emissions get-parameter-from-file word market-data-folder "ghg-emissions.csv"
  set ghg-emission-means    item 0 emissions
  set ghg-emission-sds      item 1 emissions

;  let prices-data           get-prices word market-data-folder "prices.csv"
;  set prices                item 0 prices-data
;  set environmental-taxes   item 1 prices-data
  set prices           get-prices word market-data-folder "prices.csv"
end

;; return mean and sd tables for a parameter from a file
to-report get-parameter-from-file [file]
  ;; each parameter CSV file has the same format
  ;; shown here with spaces added for clarity, and
  ;; extending to 8 LUC levels. Cols can be in any order
  ;; BUT LUC mean/sd rows MUST be as shown.
  ;;
  ;;          , SNB , Dairy, Forest, Crop
  ;; LUC1_Mean, 3500,  9500,   4000, 3000
  ;; LUC1_SD  ,  700,  1900,    800,  600
  ;; LUC2_Mean, 2650,  8050,   4000, 2850
  ;; LUC2_SD  ,  530,  1610,    800,  570
  ;; LUC3_Mean, 1625,  7350,   3850, 2675
  ;; LUC3_SD  ,  325,  1470,    770,  535
  ;;   ... and so on to LUC8 ...
  ;;
  ;; Returned tables are readable as
  ;;   table:get (table:get TABLE LUC-CODE) FARM-TYPE
  let means-table table:make
  let sds-table table:make
  carefully [
    print word "Reading parameters from " file
    file-open file
    let the-farm-types but-first csv:from-row file-read-line
    let order get-order the-farm-types farm-types
    let luc 1
    while [not file-at-end?] [
      ;; read the LUCx_Mean line
      let means-data but-first csv:from-row file-read-line
      let means table:from-list reorder zip the-farm-types means-data order
      table:put means-table luc means
      ;; read the LUCx_SD line
      let sds-data but-first csv:from-row file-read-line
      let sds table:from-list reorder zip the-farm-types sds-data order
      table:put sds-table luc sds
      set luc luc + 1
    ]
    file-close
  ]
  [
    print word "ERROR: failed while reading parameter file " file
    file-close
  ]
  report (list means-table sds-table)
end

to-report get-prices [file]
  ;; the CSV for prices and emissions taxes looks like this
  ;; (spacing addded for clarity). No checking that the rows
  ;; are in this order
  ;;
  ;;               ,SNB,Dairy, Forest, Crop
  ;;Price_Commodity,  5,  7.5,   157,   0.5
  ;;Price_GhG      , 25, 25  ,    25,  25
  ;;Price_Nleach   ,  0,  0  ,     0,   0
  ;; ... other redundant lines ...
  ;;
  ;; NOTE: the taxes part of this has been replaced by a slider
  ;; since carbon taxes are a fixed price across all landuses
  ;;
  let prices-table nobody
  let taxes-table nobody
  carefully [
    print word "Reading prices from " file
    file-open file
    let the-farm-types but-first csv:from-row file-read-line
    let order get-order the-farm-types farm-types
    let price-data but-first csv:from-row file-read-line
    set prices-table table:from-list reorder zip the-farm-types price-data order
    file-close
  ]
  [
    print "ERROR: problem reading prices file"
    file-close
  ]
;  report list prices-table taxes-table
  report prices-table
end

;; There is potential in matrix approaches for speeding up the calculations and
;; perhaps even making them more 'sophisticated'. However some of this may come
;; at the cost of (i) less readable code, and (ii) less variance. On (ii) specifically
;; there is now no base patch level variance, i.e. the mean yield, emissions and
;; input costs of every patch of given landuse and LUC combination is effectively
;; identical - not initialised with the supplied SD. The SD is applied to patches
;; each round BUT AT FARM LEVEL. That is, every patch on a given farm with given
;; LU/LUC experiences same deviation from mean outcomes in a particular year. But a
;; neighbouring farm might experience quite different deviations.
to make-matrix-copies-of-data
  ;; each of these is an 8-row 4-col matrix of the stated parameters by LUC and landuse/farm-type
  set m-yield-means    matrix:from-row-list map [t -> table:values t] table:values commodity-yield-means
  set m-yield-sds      matrix:from-row-list map [t -> table:values t] table:values commodity-yield-sds
  set m-cost-means     matrix:from-row-list map [t -> table:values t] table:values input-cost-means
  set m-cost-sds       matrix:from-row-list map [t -> table:values t] table:values input-cost-sds
  set m-emission-means matrix:from-row-list map [t -> table:values t] table:values ghg-emission-means
  set m-emission-sds   matrix:from-row-list map [t -> table:values t] table:values ghg-emission-sds

  ;; a column matrix with one element per landuse/farm-type
  set m-prices matrix:from-column-list (list table:values prices)

  ;; each of the next three is a n-row 4-col of intervention impacts, where rows are
  ;; the type of intervention and columns are landuse/farm-type
  set m-intervention-cost-impacts matrix:from-row-list
    map [i -> map [x -> replace x na-value 0] ;; convert NAs to 0
              table:values table:get table:get interventions i "costs"] intervention-types

  ;; note that these two are multiplicatively applied so add 1 to 0.05 -> 1.05, 0 -> 1, etc
  set m-intervention-yield-impacts matrix:plus 1 matrix:from-row-list
    map [i -> map [x -> replace x na-value 0] ;; convert NAs to 0
              table:values table:get table:get interventions i "yields"] intervention-types
  set m-intervention-emissions-impacts matrix:plus 1 matrix:from-row-list
    map [i -> map [x -> replace x na-value 0] ;; convert NAs to 0
              table:values table:get table:get interventions i "emissions"] intervention-types
end


;; -----------------------------------------
;; reset code
;; -----------------------------------------

to store-initial-values
  ask farm-land [
    set landuse-0 landuse
  ]
  ask farms [
    set farm-type-0 farm-type
    set current-profit-0 current-profit
    set current-income-0 current-income
    set current-costs-0 current-costs
    ;; important to copy here to get a new matrix, not a reference to the old one
    set my-interventions-0 matrix:copy my-interventions
    set available-interventions-0 matrix:copy available-interventions
  ]
end

to restore-initial-values
  ask farm-land [
    set landuse landuse-0
  ]
  ask farms [
    set farm-type farm-type-0
    set current-profit current-profit-0
    set current-income current-income-0
    set current-costs current-costs-0
    ;; important to copy here to get a new matrix, not a reference to the old one
    set my-interventions matrix:copy my-interventions-0
    set available-interventions matrix:copy available-interventions-0
  ]
  redraw-farms
  reset-ticks
  tick
  if run-rng-seed != 0 [ random-seed run-rng-seed ]
end


;; The MIT License (MIT)
;;
;; Copyright (c) 2023-24 David O'Sullivan
;;
;; Permission is hereby granted, free of charge, to any person
;; obtaining a copy of this software and associated documentation
;; files (the "Software"), to deal in the Software without restriction,
;; including without limitation the rights to use, copy, modify, merge,
;; publish, distribute, sublicense, and/or sell copies of the Software,
;; and to  permit persons to whom the Software is furnished to do so,
;; subject to the following conditions:
;;
;; The above copyright notice and this permission notice shall be included
;; in all copies or substantial portions of the Software.
;;
;; THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS
;; OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
;; FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL
;; THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
;; LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
;; FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER
;; DEALINGS IN THE SOFTWARE.