;; ----------------------------------------------------------------------------
;; many scenario and spatial setup parameters are in files not the UI
;;
;; most of the 'action' here now happens in table-utils
;; ----------------------------------------------------------------------------

;; initialise various folders - here for convenient modification in one place
to setup-folders
  let base-data-folder    join-string (list pathdir:get-model-path "data") pathdir:get-separator
  set output-data-folder  join-string (list base-data-folder "output")     pathdir:get-separator
  set market-data-folder  join-string (list base-data-folder "market")     pathdir:get-separator
  set spatial-data-folder join-string (list base-data-folder "spatial")    pathdir:get-separator
end


;; ----------------------------------------------------------------------------
;; farmer (behavioural) parameter setup
;; ----------------------------------------------------------------------------
to setup-farmer-parameters
  set env-metrics              read-multi-row-table join-string (list market-data-folder "environmental-metrics.csv")      pathdir:get-separator false
  set farm-types              sort get-column-names join-string (list market-data-folder "farmer-threshold-matrix.csv")    pathdir:get-separator 2
  set dispositions table:to-list read-one-row-table join-string (list market-data-folder "farmer-dispositions.csv")        pathdir:get-separator false
  set mgmt-intervention-types sort get-column-names join-string (list market-data-folder "intervention-initial-props.csv") pathdir:get-separator 2
  set intervention-adoption    read-three-way-table join-string (list market-data-folder "intervention-initial-props.csv") pathdir:get-separator false
  set base-thresholds          read-three-way-table join-string (list market-data-folder "farmer-threshold-matrix.csv")    pathdir:get-separator false
  set farm-type-change-probs  read-transition-table join-string (list market-data-folder "conversion-probabilities.csv")   pathdir:get-separator
  read-intervention-impacts-from-files
  foreach mgmt-intervention-types [ i -> output-print i ]
end


to read-intervention-impacts-from-files
  set mgmt-interventions table:make 
  let impacts-to-read (sentence ["costs" "yields"] table:keys env-metrics)
  foreach impacts-to-read [ impact ->
    let fname join-list (list market-data-folder (word impact "-intervention-impacts.csv")) pathdir:get-separator
    let impacts read-multi-row-table fname false
    table:put mgmt-interventions impact impacts
  ] 
end


;; ----------------------------------------------------------------------------
;; economic setup procedures
;; ----------------------------------------------------------------------------
to setup-economic-parameters
  set yield-means matrix:from-row-list map [t -> table:values t] table:values table:get get-yields "Mean"
  set yield-sds   matrix:from-row-list map [t -> table:values t] table:values table:get get-yields "SD"
  set cost-means  matrix:from-row-list map [t -> table:values t] table:values table:get get-costs  "Mean"
  set cost-sds    matrix:from-row-list map [t -> table:values t] table:values table:get get-costs  "SD"
  setup-costs-yields-time-series
  set prices      col-matrix get-prices
  set prices-time-series get-prices-time-series
  setup-environmental-metrics
end

;;
;; Note that these are all setup as reporters to make it easier to test them from the console
;;
to-report get-yields
  report read-three-way-table join-string (list market-data-folder "yields.csv") pathdir:get-separator false
end

to-report get-costs
  report read-three-way-table join-string (list market-data-folder "costs.csv") pathdir:get-separator false
end

to setup-costs-yields-time-series
  let tbl read-three-way-table join-string (list market-data-folder "costs-yields-time-series.csv") pathdir:get-separator true
  set cost-means-time-series  table:get table:get tbl "Mean" "costs"
  set cost-sds-time-series    table:get table:get tbl "SD"   "costs"
  set yield-means-time-series table:get table:get tbl "Mean" "yields"
  set yield-sds-time-series   table:get table:get tbl "SD"   "yields"
end


to setup-environmental-metrics
  set env-metric-means table:make
  set env-metric-sds table:make
  foreach sort table:keys env-metrics [ metric ->
    let metric-vals read-three-way-table join-string (list market-data-folder (word metric ".csv")) pathdir:get-separator false
    table:put env-metric-means metric matrix:from-row-list map [t -> table:values t] table:values table:get metric-vals "Mean"
    table:put env-metric-sds   metric matrix:from-row-list map [t -> table:values t] table:values table:get metric-vals "SD"
  ]
end


to-report get-prices
  report read-one-row-table join-string (list market-data-folder "prices.csv") pathdir:get-separator true
end

to-report get-prices-time-series
  let prices-ts get-table-transpose read-multi-row-table join-string (list market-data-folder "prices-time-series.csv") pathdir:get-separator false
  report map [x -> col-matrix x] 
         table:values 
         table:from-list (map list table:keys prices-ts
                                   map [tbl -> table:values tbl] table:values prices-ts)
end


;; There is potential in matrix approaches for speeding up the calculations, perhaps even making them more 
;; 'sophisticated'. However some of this may come at the cost of 
;;
;;   (i) less readable code, and 
;;  (ii) less variance in outcomes
;; 
;; On (ii) specifically there is now no base patch level variance, i.e. the mean yield, emissions
;; and input costs of every patch of given landuse and LUC combination is effectively identical - not 
;; initialised with the supplied SD. The SD is applied to patches each round BUT AT HOLDING LEVEL. That is,
;; every patch on a given holding with given LU/LUC experiences same deviation from mean outcomes in a 
;; particular year. But a neighbouring holding might experience quite different deviations.
to make-matrix-copies-of-some-data
  set intervention-cost-impacts  intervention-farm-type-tbl-to-mtx mgmt-interventions "costs" false
  set intervention-yield-impacts intervention-farm-type-tbl-to-mtx mgmt-interventions "yields" true
  set intervention-env-impacts table:make
  foreach sort table:keys env-metrics [ metric ->
    table:put intervention-env-impacts metric intervention-farm-type-tbl-to-mtx mgmt-interventions metric true
  ]
end

to-report intervention-farm-type-tbl-to-mtx [tbl impact multiplicative?]
  ;; impact will be one of 'costs', 'yields', 'ghg-emissions' or any of the environmental-impacts
  let impacts table:get tbl impact
  let rows []
  foreach mgmt-intervention-types [ intervention ->
    let impacts-by-farm-type table:get impacts intervention
    let effects map [ft -> table:get impacts-by-farm-type ft] farm-types
    set effects map [x -> replace x na-value 0] effects
    if multiplicative? [
      set effects map [x -> x + 1] effects
    ]
    set rows lput effects rows
  ]
  report matrix:from-row-list rows
end


;; The MIT License (MIT)
;;
;; Copyright (c) 2023-25 David O'Sullivan
;;
;; Permission is hereby granted, free of charge, to any person
;; obtaining a copy of this software and associated documentation
;; files (the "Software"), to deal in the Software without restriction,
;; including without limitation the rights to use, copy, modify, merge,
;; publish, distribute, sublicense, and/or sell copies of the Software,
;; and to  permit persons to whom the Software is furnished to do so,
;; subject to the following conditions:
;;
;; The above copyright notice and this permission notice shall be included
;; in all copies or substantial portions of the Software.
;;
;; THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS
;; OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
;; FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL
;; THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
;; LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
;; FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER
;; DEALINGS IN THE SOFTWARE.