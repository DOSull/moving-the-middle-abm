;; The main purpose of this breed is to aggregate income, costs, and profits and store them
;; TO CONSIDER: Potentially should be moved to the farmer since this breed does not do much!

farms-own [
  my-farmer               ;; farmer who owns/runs farm

  the-land                ;; patch-set of patches of this farmer, this farm, or this holding
  farm-type               ;; farm type of farm - one of farm-types global

  gross-profit
  current-profit          ;; most recent profit of farm summed across patches
  current-income          ;; most recent income of farm summed across patches
  current-costs           ;; most recent costs of farm summed across patches
  my-metrics
  
  farm-type-0
  gross-profit-0
  current-profit-0        ;; variables at model initialisation for quick reset
  current-income-0
  current-costs-0
  my-metrics-0
]

;; farm 'constructor'
to initialise-farm
  ;; will be called by the farmer who is 'myself' in this context
  set my-farmer myself
  set shape "circle 3"
  set hidden? false
  set label farm-type
  set label-color ifelse-value show-labels? [table:get colour-key "label"] [[0 0 0 0]]
  set my-metrics table:make
end

to update-farm-environmental-metrics
  foreach sort table:keys env-metrics [ metric ->
    table:put my-metrics metric sum [table:get my-metrics metric] of [my-holdings] of my-farmer
  ]
end

;; note that this is also runnable by a holding...
to-report is-in-breach? [metric]
  report table:get my-metrics metric / count the-land > table:get (table:get env-metrics metric) "limit"
end

;; calculates income and costs by summing holding contributions
;; calculate gross profit (before loan repayments) based on this
to update-farm-income-and-costs
  set current-income sum [current-income] of [my-holdings] of my-farmer
  set current-costs sum [current-costs] of [my-holdings] of my-farmer
  set gross-profit current-income - current-costs
end

;; estimate value based on base returns of holdings summed
;; the multiple here is arbitrary
;; NOTE: making est value dependent on interest rates tends to 
;; mean that aggregate loan payments are stable regardless of interest rates
;; because mortgages end up larger... this may need further consideration
to-report get-estimated-farm-value
  let est-profit sum [get-gross-return] of [my-holdings] of my-farmer
  report round (est-profit * 100 / discount-rate)
end


;;
;; DEBUG FILE OUTPUT
;; Note this preferred to using simpleR extension to avoid dependency on R installation
to output-farms-report [fname]
  csv:to-file fname fput get-farm-csv-header map [h -> [get-farm-status] of h] sort farms
end

to-report get-farm-status
  report flatten-list (list 
    who
    [who] of my-farmer
    farm-type 
    (count the-land * hectares-per-patch)
    gross-profit
    current-profit
    current-income
    current-costs
    (map [m -> precision table:get my-metrics m 1] table:keys env-metrics)
  )
end

to-report get-farm-csv-header
  report flatten-list (list
    "farm_id"
    "farmer_id"
    "farm_type"
    "size_ha"
    "gross_profit"
    "current_profit"
    "current_income"
    "current_costs"
    table:keys env-metrics
  )
end


;; The MIT License (MIT)
;;
;; Copyright (c) 2023-25 David O'Sullivan
;;
;; Permission is hereby granted, free of charge, to any person
;; obtaining a copy of this software and associated documentation
;; files (the "Software"), to deal in the Software without restriction,
;; including without limitation the rights to use, copy, modify, merge,
;; publish, distribute, sublicense, and/or sell copies of the Software,
;; and to  permit persons to whom the Software is furnished to do so,
;; subject to the following conditions:
;;
;; The above copyright notice and this permission notice shall be included
;; in all copies or substantial portions of the Software.
;;
;; THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS
;; OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
;; FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL
;; THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
;; LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
;; FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER
;; DEALINGS IN THE SOFTWARE.
